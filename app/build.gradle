apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "25.0.1"
    defaultConfig {
        applicationId "io.github.mchav.tryfrege"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            debuggable false
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/frege/'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    testCompile 'junit:junit:4.12'
}

project.afterEvaluate {
    extensions.compileFrege = {
        description = 'Compile Frege to Java'
        javaexec {
            /* def fregeCompiler = "C:\\Users\\mscha\\Frege\\frege-3.24.100.1-jdk7.jar"
             classpath += files(fregeCompiler) */

            configurations.compile.resolve().each {
                classpath += files(it.toString())
            }

            main = 'frege.compiler.Main'
            // list of all aar dependencies
            def rootDir = project.rootDir
            def localProperties = new File(rootDir, "local.properties")
            def androidJarPath = ""
            if (localProperties.exists()) {
                Properties properties = new Properties()
                localProperties.withInputStream { instr ->
                    properties.load(instr)
                }
                def sdkDir = properties.getProperty('sdk.dir')
                androidJarPath = sdkDir +
                        String.format("/platforms/%s/android.jar",
                                android.compileSdkVersion).replace('/' as char, File.separatorChar)
            }

            // 'mockAndroidJar' for compilation
            classpath += files(androidJarPath)
            def list = [androidJarPath]
            def applicationId = (android.defaultConfig.applicationId + ".").replace('.' as char, File.separatorChar)
            def dir = new File(rootProject.projectDir.path +
                    "/app/build/intermediates/exploded-aar/com.android.support/".replace('/' as char, File.separatorChar))
            dir.eachFileRecurse (groovy.io.FileType.FILES) { file ->
                if (file.path.endsWith(".jar")) {
                    list << file.path
                    classpath += files(file.path)
                }
            }

            def appPath = "src/main/frege/".replace('/' as char, File.separatorChar) + applicationId
            def a = ['-j', '-target', '1.7', '-v', '-inline', '-O', '-d', 'src/main/java', '-make',
                     '-fp', list.join(';'),
                     '-sp', appPath, appPath ]
            println 'Frege compiler args: "' + a.join(' ') + '"'
            args (*a)
        }
    }

    try {
        // compileFrege()
    } catch (Exception e) {
        // all error handling is shown on the gradle console.
        System.err << "Frege compilation failed."
    }
}

